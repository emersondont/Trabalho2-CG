<!DOCTYPE html>
<html>

<head>
    <meta charset="utf-8">
    <title>Ray Marching Example</title>
    <style>
        body {
            display: flex;
            justify-content: center;
            align-items: center;
            gap: 10%;
            background-color: #292837;
            /*cursor: none;*/
        }

        canvas {
            width: 96vh;
            height: 96vh;
            border: 2px solid #22202c;
        }
        .item {
            display: flex;
            flex-direction: row;
            gap: 24px;
            align-items: center;
            justify-content: space-between;
            color: #fff;
            font-weight: 500;
            font-size: 20px;
        }
        .item p {
            margin-bottom: 5%;
        }

        .wasd {
            display: flex;
            flex-direction: column;
            justify-content: center;
            align-items: center;
            font-weight: 900;
            font-size: 24px;
        }
        .wasd p {
            margin: 0;
        }

        .inputs {
            display: flex;
            flex-direction: column;
        }
        .inputs div {
            display: flex;
            flex-direction: row;
            gap: 16px;
        }

        .inputs p {
            font-weight: 500;
            font-size: 20px;
            color: #fff;
        }

        .inputs input {
            width: 100px;
        }
    </style>

</head>

<body>
    <div class="buttons">
        <div class="item">
            <div class="wasd">
                <p>W</p>
                <p>A S D</p>
            </div>
            <p>Move</p>
        </div>

        <div class="item">
            <p style="font-weight: 900; font-size: 24px;">Shift</p>
            <p>Up</p>
        </div>
        
        <div class="item">
            <p style="font-weight: 900; font-size: 24px;">Control</p>
            <p>Down</p>
        </div>

        <div class="item">
            <p style="font-weight: 900; font-size: 24px;">Space</p>
            <p>Pause</p>
        </div>
    </div>

    <canvas id="canvas"></canvas>

    <div class="inputs">
        <div>
            <input type="range" min="1.5" max="6.0" step="0.05" id="inputsmooth">
            <p>Smooth</p>
        </div>
        <div>
            <input type="range" min="0.1" max="1.0" step="0.01" id="inputvel">
            <p>Speed</p>
        </div>
    </div>

    <script type="x-shader/x-vertex" id="vertexShader">
        precision mediump float;
        attribute vec3 a_position;
        void main() {
          gl_Position = vec4(a_position, 1.0);
        }
    </script>
    <script type="x-shader/x-fragment" id="fragmentShader">
        precision mediump float;
        
        uniform vec2 u_resolution;
        uniform float u_time;
        uniform float u_xCam;
        uniform float u_yCam;
        uniform float u_zCam;
        uniform float u_k;

        vec3 lightPosition = vec3(0.0, 0.0, 6.0);
        vec3 lightColor = vec3(1);
        float lightIntensity = 14.0;
        float lightAttenuation = 0.6;

        mat2 rotate(float a) {
            float s = sin(a);
            float c = cos(a);

            return mat2(c, -s, s, c);
        }

        float sphereSDF(vec3 p, vec3 c, float r) {
            return length(p - c) - r;
        }

        float cubeSDF(vec3 p, vec3 pCube, vec3 len) {
            vec3 d = abs(vec3(p.x - pCube.x, p.y - pCube.y, p.z - pCube.z)) - len;
            float insideDistance = min(max(d.x, max(d.y, d.z)), 0.0);
            float outsideDistance = length(max(d, 0.0));
            
            return insideDistance + outsideDistance;
        }        
        
        float smoothMax(float a, float b, float k) {
            return log(exp(k * a) + exp(k * b)) / k;
        }
        
        float smoothMin(float a, float b, float k) {
            return -smoothMax(-a, -b, k);
        }

        vec3 sphereRotate(vec3 position, float vel, vec3 refPosition) {
            float angle = u_time / vel;

            vec3 rotatedPosition = vec3(
                position.x * cos(angle) + position.z * sin(angle),
                position.y,
                -position.x * sin(angle) + position.z * cos(angle)
            );

            vec3 finalPosition = vec3(
                rotatedPosition.x * cos(0.0) - rotatedPosition.y * sin(0.0),
                rotatedPosition.x * sin(0.0) + rotatedPosition.y * cos(0.0),
                rotatedPosition.z
            ) + refPosition;

            return finalPosition;
        }

        float opSmoothUnion(float d1, float d2, float k ) {
            float h = clamp( 0.5 + 0.5*(d2-d1)/k, 0.0, 1.0 );
            return mix( d2, d1, h ) - k*h*(1.0-h);
        }
                
        vec4 scene(vec3 p) {
            const int len = 20;
            vec3 bp = p;
            bp.xy *= rotate(u_time/10.0);

            float d1 = max(
                cubeSDF(vec3(bp.x, bp.y, bp.z), vec3(0.0, 0.0, -1.0), vec3(5.0, 5.0, 5.0)),
                -cubeSDF(vec3(bp.x, bp.y, bp.z), vec3(0.0, 0.0, 0.0), vec3(5.0, 5.0, 5.0))
            );
            vec3 color = vec3(0.8, 0.0000, 0.0627);
            float distance = d1;

            vec3 colors[len];
            colors[0] = vec3(1.0000, 0.2353, 0.0000);
            colors[1] = vec3(1.0000, 0.7333, 0.0000);
            colors[2] = vec3(1.0000, 0.9176, 0.0000);
            colors[3] = vec3(0.2980, 0.6824, 0.3098);
            colors[4] = vec3(0.0118, 0.5961, 0.8863);
            colors[5] = vec3(1.0000, 0.0000, 1.0000);
            colors[6] = vec3(0.0000, 1.0000, 1.0000);
            colors[7] = vec3(1.0000, 1.0000, 0.0000);
            colors[8] = vec3(1.0000, 0.0000, 0.5020);
            colors[9] = vec3(0.0000, 1.0000, 0.0000);
            colors[10] = vec3(1.0000, 0.2353, 0.0000);
            colors[11] = vec3(1.0000, 0.7333, 0.0000);
            colors[12] = vec3(1.0000, 0.9176, 0.0000);
            colors[13] = vec3(0.2980, 0.6824, 0.3098);
            colors[14] = vec3(0.0118, 0.5961, 0.8863);
            colors[15] = vec3(1.0000, 0.0000, 1.0000);
            colors[16] = vec3(0.0000, 1.0000, 1.0000);
            colors[17] = vec3(1.0000, 1.0000, 0.0000);
            colors[18] = vec3(1.0000, 0.0000, 0.5020);
            colors[19] = vec3(0.0000, 1.0000, 0.0000);
            
            for(int i = 0; i < len; i++) {
                float fi = float(i);
		        float time = u_time * (fract(fi * 412.531 + 0.513) - 0.5) * 2.0;

                float d2 = sphereSDF(
                    p + sin(time + fi * vec3(52.5126, 64.62744, 632.25)) * vec3(2.0, 2.0, 0.8), 
                    vec3(0.0),
                    mix(0.5, 1.0, fract(fi * 412.531 + 0.5124))
                );

                //vec3 colorAux = vec3(0.0, 0.0, float(i)/float(len));
  
                float mix_factor = d1 / (d2 + d1);
                color = mix(
                    color,
                    colors[i],
                    mix_factor
                );
                d1 = distance;
                distance = smoothMin(distance, d2, u_k);
            }

            return vec4(color, distance); 
        }

        vec3 estimateNormal(vec3 p) {
            float eps = 0.001;
            float dx = scene(vec3(p.x + eps, p.y, p.z)).w - scene(vec3(p.x - eps, p.y, p.z)).w;
            float dy = scene(vec3(p.x, p.y + eps, p.z)).w - scene(vec3(p.x, p.y - eps, p.z)).w;
            float dz = scene(vec3(p.x, p.y, p.z + eps)).w - scene(vec3(p.x, p.y, p.z - eps)).w;
            return normalize(vec3(dx, dy, dz));
        }
        
        float calculateLight(vec3 pointPos, vec3 pointNormal) {
            // Calcula a direção da luz em relação ao ponto
            vec3 lightDirection = normalize(lightPosition - pointPos);
          
            // Calcula o produto escalar entre a direção da luz e a normal do ponto
            float diff = max(dot(pointNormal, lightDirection), 0.02);
          
            // Calcula a atenuação da luz em função da distância
            float distance = length(lightPosition - pointPos);
            float attenuation = 1.0 / (1.0 + lightAttenuation * distance * distance);
            
            //calcula a intencidade da specular
            vec3 reflectedDirection = reflect(-lightDirection, pointNormal);
            float cosAngle = max(dot(reflectedDirection, pointNormal), 0.5);
            float specularIntensity = pow(cosAngle, 10.0);

            // Retorna a quantidade de luz que atinge o ponto
            return lightIntensity * attenuation * (diff + specularIntensity);
        }

        float calculateShadow(vec3 pointPos) {
            // Calcula a direção do raio
            vec3 shadowDirection = normalize(lightPosition - pointPos);
            float distanceLength = length(pointPos - lightPosition);
            float radius = 0.5;

            // fraction of light visible, starts at one radius (second half added in the end);
	        float lf = radius * distanceLength;
	
            // distance traveled
            float dt = 0.01;

            for (int i = 0; i < 100; i++) {
                vec3 currentPos = pointPos + shadowDirection * dt;
                float currentDistance = scene(currentPos).w;
                if (currentDistance < -radius) {
                    return 0.0;
                    break;
                }
                
                lf = min(lf, currentDistance / dt);
                dt += currentDistance;

                if (dt > distanceLength) {
                    break;
                }
            }

            lf = clamp((lf*distanceLength + radius) / (2.0 * radius), 0.0, 1.0);
	        lf = smoothstep(0.0, 1.0, lf);
        
            return lf;
        }

        vec3 raymarch(vec3 ro, vec3 rd) {
            float t = 0.0;
            float d = 0.0;  //distance
            vec3 color;
            float lightAmount;
            float shadow = 1.0;
            
            for (int i = 0; i < 100; i++) {
                vec3 p = ro + rd * t;
                vec4 sc = scene(p);
                d = sc.w;
                color = sc.xyz;
                
                t += d;
                if (d < 0.001 || t > 100.0) {
                    //lightPosition = sphereRotate(lightPosition, 0.8, vec3(0.0, 0.0, 10.0));
                    vec3 normal = estimateNormal(p);
                    lightAmount = calculateLight(p, normal);
                    shadow = calculateShadow(p);
                    break;
                }
            }
            if(t >= 100.0) {
                return vec3(0.1333, 0.1255, 0.1725);
            }
            
            return color * lightAmount * lightColor * shadow;
            //return color * lightAmount * shadow;
        }

        void main() {
            vec2 uv = gl_FragCoord.xy / u_resolution.xy;
            vec3 pointToLookAt = vec3(0.5, 0.5, 1.0);
            //ray origin
            vec3 ro = vec3(u_xCam, u_yCam, u_zCam);
            //ray direction
            vec3 rd = normalize(vec3(uv, 0.0) - pointToLookAt);
            vec3 color = raymarch(ro, rd);
            gl_FragColor = vec4(color, 1.0);
        }
    </script>

    <script src="index.js"></script>
</body>

</html>